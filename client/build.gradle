import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

plugins {
    id "java"
    id "application"
    id "org.springframework.boot"
    id "com.google.protobuf"
    id "com.bmuschko.docker-remote-api"
}

dependencies {
    protobuf project(':service-idl')

    implementation "com.netifi:netifi-spring-boot-starter"
}

mainClassName = 'example.client.ClientApplication'

// Packaging
task buildImage(type: DockerBuildImage, dependsOn: build) {
    buildArgs = ['JAR_FILE': "${bootJar.archiveName}"]
    dockerFile = file("${projectDir}/Dockerfile")
    inputDir = file("${projectDir}")
    tags = ["netifi-broadcast-example/${projectDir.name}", "netifi-broadcast-example/${projectDir.name}:${version}"]
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc'
    }
    plugins {
        rsocketRpc {
            artifact = 'io.rsocket.rpc:rsocket-rpc-protobuf'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                rsocketRpc {}
            }
        }
    }
}